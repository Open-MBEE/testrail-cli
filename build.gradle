group 'org.openmbee.testrail'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8

String buildTag = project.getProperties().get('buildTag')
if (buildTag != null && buildTag != version) {
    throw new GradleException('Version mismatch.')
}
if (buildTag == null || buildTag.isEmpty() || project.getProperties().getOrDefault('artifactoryRepository', '').toLowerCase().contains('snapshot')) {
    version += '-SNAPSHOT'
}
String buildNumber = project.getProperties().getOrDefault('buildNumber', System.currentTimeSeconds());

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.4.0'
    }
}

repositories {
    jcenter()
}

dependencies {
    compile "org.projectlombok:lombok:1.16.16"

    compile group: 'org.openmbee.junit', name: 'junit-xml-parser', version: '1.0.0'
    compile group: 'org.openmbee.testrail', name: 'testrail-client-wrapper', version: '1.0.0'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

artifactory {
    contextUrl = project.getProperties().get('artifactoryUrl')
    publish {
        repository {
            repoKey = project.getProperties().get('artifactoryRepository')
            username = project.getProperties().get('artifactoryUsername')
            password = project.getProperties().get('artifactoryPassword')
        }
        defaults {
            publications('mavenJava')
        }
        clientConfig.setIncludeEnvVars(true)
        if (buildNumber != null && !buildNumber.isEmpty()) {
            clientConfig.info.setBuildNumber(buildNumber)
        }
    }
}
artifactoryPublish.dependsOn(assemble, sourcesJar, javadocJar)